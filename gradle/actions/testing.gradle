apply plugin: 'jacoco'

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
check.dependsOn jacocoTestReport

task checkForExtensionFile {
    onlyIf {
        project.name != "OrchidCore" && project.name != "OrchidTest"
    }
    doLast {
        def extensionName = project.name.toString()
        def extensionType = project.parent.name.toString()
        if(extensionType == 'languageExtensions') {
            extensionType = 'plugins'
        }
        if(extensionType == 'integrations') {
            extensionType = 'plugins'
        }
        def fullExtensionFilename = "${rootProject.rootDir}/OrchidCore/src/orchid/resources/pages/${extensionType}/${extensionName}/index.md"
        def extensionFile = file(fullExtensionFilename)

        if(!extensionFile.exists()) {
            throw new FileNotFoundException("There is no plugin entry for this plugin, expected 'pages/${extensionType}/${extensionName}/index.md' to exist in OrchidCore Orchid resources.")
        }
    }
}
check.dependsOn checkForExtensionFile